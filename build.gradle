plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.4.16"
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

static def gitTag() {
    def tag = ""
    def proc = "git describe --tags".execute()
    proc.in.eachLine { line -> tag = line }
    proc.waitFor()

    if (tag ==~ /v\d+\.\d+\.\d+/)
        return [tag.substring(1), "default"]

    def branch = ""
    proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.waitFor()

    [branch, "branch"]
}

def (String ver, String channel) = gitTag()

group 'com.ypwang.plugin'
version ver

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.3.3'
    type 'IU'
    plugins 'org.jetbrains.plugins.go:193.6494.35.125'
    intellij.updateSinceUntilBuild false
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes new File('change-notes.htm').text
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
    channels channel
}