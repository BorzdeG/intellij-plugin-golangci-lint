plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.5.0"
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
}

static def gitTag() {
    def tag = ""
    def proc = "git describe --tags".execute()
    proc.in.eachLine { line -> tag = line }
    proc.waitFor()

    if (tag ==~ /v\d+\.\d+\.\d+/)
        return [tag.substring(1), "default"]

    def branch = ""
    proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.waitFor()

    [branch, "branch"]
}

def (String ver, String channel) = gitTag()

group 'com.ypwang.plugin'
version ver

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '202.7660.26'
    type 'IU'
    plugins = ['org.jetbrains.plugins.go:202.7660.3', 'com.intellij.plugins.watcher:202.6397.21']
    intellij.updateSinceUntilBuild false
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        // Disable optimized callable references. See https://youtrack.jetbrains.com/issue/KT-37435
        freeCompilerArgs += "-Xno-optimized-callable-references"
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

static def changeLog() {
    def all = new File('change-notes.htm').text
    def count = 0
    def idx = -1

    // keep at most 5 recent change logs
    while (count < 6) {
        idx = all.indexOf("<p>", idx+1)
        if (idx == -1)
            return all

        count++
    }

    return all.substring(0, idx) + """\n<a href="https://github.com/xxpxxxxp/intellij-plugin-golangci-lint/blob/master/change-notes.htm">Previous...</a>"""
}

patchPluginXml {
    changeNotes changeLog()
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
    channels channel
}

runIde {
    maxHeapSize = "4g"
}